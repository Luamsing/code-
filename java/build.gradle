buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group 'net.jeedup.common'
    version '1.0.' + (System.getenv('BUILD_NUMBER') ?: '0-SNAPSHOT')

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0'
    }
}

task all(dependsOn: ['clean', 'jar', 'test'])
task clean(dependsOn: getTasksByName('clean', true))
task jar(dependsOn: getTasksByName('jar', true))
task test(dependsOn: getTasksByName('test', true))
task pub(dependsOn: getTasksByName('publishToMavenLocal', true))

project(':common') {
    publishing {
        publications {
            api(MavenPublication) {
                groupId group
                artifactId 'common'
                version version
                from components.java
            }
        }
    }
}

project(':json') {
    dependencies {
        // Common
        compile project(':common')

        // Jackson
        compile 'com.fasterxml.jackson.core:jackson-core:2.7+'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.7+'
        compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.7.+'
    }
    publishing {
        publications {
            api(MavenPublication) {
                groupId group
                artifactId 'json'
                version version
                from components.java
            }
        }
    }
}

project(':kinesis') {
    dependencies {
		// Common
        compile project(':common')

        // JSON common
        compile project(':json')

        // AWS Kinesis Client
        compile 'com.amazonaws:amazon-kinesis-client:1.+'

        // AWS Kinesis Lambda Events
        compile 'com.amazonaws:aws-lambda-java-events:1+'
    }
    publishing {
        publications {
            api(MavenPublication) {
                groupId group
                artifactId 'kinesis'
                version version
                from components.java
            }
        }
    }
}

project(':app') {

    def mainClassName = 'net.jeedup.app.App'

    dependencies {
        compile project(':common')
        compile project(':json')
        //compile 'net.jeedup.common:common:1+'
        //compile 'net.jeedup.common:json:1+'
    }

    jar {
        dependsOn configurations.runtime
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
        manifest {
            attributes 'Main-Class': mainClassName
        }
    }
}
